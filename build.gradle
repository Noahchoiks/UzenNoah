buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
        queryDSLVersion = '3.6.7'
    }
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.mysema.querydsl:querydsl-apt:${queryDSLVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'

jar {
    baseName = 'g1project'
    version =  '1.0'
}

repositories {
    maven { url "http://repo.spring.io/libs-milestone" }
    mavenCentral()    
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    providedRuntime
    querydslapt
}

dependencies {

	//APACHE COMMON
    compile("commons-beanutils:commons-beanutils")
    compile("commons-collections:commons-collections")
	compile('commons-io:commons-io:2.4')
	compile('commons-lang:commons-lang:2.1')

	//SPRING BOOT
    compile('org.springframework.boot:spring-boot-devtools')
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-rest")    
    compile("org.springframework.boot:spring-boot-starter-data-jpa")    
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')

	//QUERYDSL
	compile("com.mysema.querydsl:querydsl-jpa:${queryDSLVersion}")
	compile("com.mysema.querydsl:querydsl-apt:${queryDSLVersion}")
	//querydslapt("com.mysema.querydsl:querydsl-apt:${queryDSLVersion}")

	//TOMCAT(BOOT)
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

	//H2 DB
    runtime('com.h2database:h2')
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
